module(name = "com_github_buildbarn_bb_browser")

bazel_dep(name = "aspect_rules_js", version = "1.39.1")
bazel_dep(name = "com_github_buildbarn_bb_remote_execution")
bazel_dep(name = "com_github_buildbarn_bb_storage")
bazel_dep(name = "com_github_buildbarn_go_xdr")
bazel_dep(name = "gazelle", version = "0.35.0")
bazel_dep(name = "protobuf", version = "23.1")
bazel_dep(name = "rules_go", version = "0.46.0")
bazel_dep(name = "rules_jsonnet", version = "0.5.0")
bazel_dep(name = "rules_proto", version = "6.0.0-rc2")
bazel_dep(name = "toolchains_llvm", version = "1.0.0")

git_override(
    module_name = "com_github_buildbarn_bb_remote_execution",
    commit = "ea22f37f62b885026ad9e1aa0d62deb15dbb454a",
    remote = "https://github.com/buildbarn/bb-remote-execution.git",
)

git_override(
    module_name = "com_github_buildbarn_bb_storage",
    commit = "dee7e428a0bd962b192512aad8f3ff76dcc0f437",
    remote = "https://github.com/buildbarn/bb-storage.git",
)

git_override(
    module_name = "com_github_buildbarn_go_xdr",
    commit = "9cb8426d3e09e301c2b12c993e54ef795a665a92",
    remote = "https://github.com/buildbarn/go-xdr.git",
)

git_override(
    module_name = "gazelle",
    commit = "f5a5c5dc90b4d6a39f0f47d770eba27c6c1c1877",
    patches = ["//:patches/gazelle/googleapis.diff"],
    remote = "https://github.com/bazelbuild/bazel-gazelle.git",
)

git_override(
    module_name = "rules_antlr",
    commit = "89a29cca479363a5aee53e203719510bdc6be6ff",
    patches = [
        "//:patches/rules_antlr/antlr-4.10.diff",
        "//:patches/rules_antlr/bzlmod.diff",
    ],
    remote = "https://github.com/marcohu/rules_antlr.git",
)

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(
    go_deps,
    "cc_mvdan_gofumpt",
    "com_github_bazelbuild_buildtools",
    "com_github_bazelbuild_remote_apis",
    "com_github_buildkite_terminal_to_html",
    "com_github_dustin_go_humanize",
    "com_github_gorilla_mux",
    "com_github_jmespath_go_jmespath",
    "com_github_kballard_go_shellquote",
    "org_golang_google_grpc",
    "org_golang_google_protobuf",
    "org_golang_x_lint",
    "org_gonum_v1_plot",
)

go_deps_dev = use_extension("@gazelle//:extensions.bzl", "go_deps", dev_dependency = True)
go_deps_dev.gazelle_override(
    build_file_generation = "on",
    directives = [
        "gazelle:go_grpc_compilers @io_bazel_rules_go//proto:go_proto,@io_bazel_rules_go//proto:go_grpc_v2",
        "gazelle:resolve proto go build/bazel/semver/semver.proto //build/bazel/semver",
        "gazelle:resolve proto go google/api/annotations.proto @org_golang_google_genproto_googleapis_api//annotations",
        "gazelle:resolve proto go google/longrunning/operations.proto @com_google_cloud_go_longrunning//autogen/longrunningpb",
        "gazelle:resolve proto go google/rpc/status.proto @org_golang_google_genproto_googleapis_rpc//status",
        "gazelle:resolve proto google/api/annotations.proto @googleapis//google/api:annotations_proto",
        "gazelle:resolve proto google/longrunning/operations.proto @googleapis//google/longrunning:operations_proto",
        "gazelle:resolve proto google/rpc/status.proto @googleapis//google/rpc:status_proto",
    ],
    path = "github.com/bazelbuild/remote-apis",
)
go_deps_dev.module_override(
    patches = ["//:patches/com_github_buildkite_terminal_to_html/assets.diff"],
    path = "github.com/buildkite/terminal-to-html",
)
go_deps_dev.module_override(
    patches = ["@com_github_buildbarn_bb_storage//:patches/com_github_grpc_ecosystem_go_grpc_prometheus/client-metrics-prevent-handled-twice.diff"],
    path = "github.com/grpc-ecosystem/go-grpc-prometheus",
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "com_github_twbs_bootstrap",
    build_file_content = """exports_files(["css/bootstrap.min.css", "js/bootstrap.min.js"])""",
    sha256 = "395342b2974e3350560e65752d36aab6573652b11cc6cb5ef79a2e5e83ad64b1",
    strip_prefix = "bootstrap-5.1.0-dist",
    urls = ["https://github.com/twbs/bootstrap/releases/download/v5.1.0/bootstrap-5.1.0-dist.zip"],
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)
llvm.toolchain(
    llvm_version = "14.0.0",
)
use_repo(llvm, "llvm_toolchain_llvm")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "com_github_buildbarn_bb_browser_npm",
    link_workspace = "com_github_buildbarn_bb_browser",
    pnpm_lock = "//:pnpm-lock.yaml",
)
use_repo(npm, "com_github_buildbarn_bb_browser_npm")
